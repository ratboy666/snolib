<h1>BALX - BAL extended</h1>

[[BALX(PARENS,QTS)]] will return a pattern which will match all
non-null balanced strings where [[PARENS]] is used to specify paired
brackets in nested fashion and [[QTS]] specifies characters to be
used as quotes.  Bracket characters within quotes are ignored for
balancing purposes.

[[BALX('(<[]>)', "'" '"')]] will match a balanced string in SNOBOL4
source.

<h2>Uses</h2>
<a href="UNIQUE.html">UNIQUE</a><br>

<h2>See Also</h2>
<a href="FASTBAL.html">FASTBAL</a>

<<INCLUDES>>=
-INCLUDE 'UNIQUE.INC'
@

<<BALX>>=
-PUBLIC BALX()
*
         DEFINE('BALX(PARENS,QTS)Q,GBAL,NAME,STAR,LP,RP')    :(BALX_END)
*
BALX     NAME = UNIQUE()
         STAR = CONVERT(NAME, 'EXPRESSION')
         GBAL = NOTANY(PARENS QTS)
BALX_1   QTS LEN(1) . Q =                                     :F(BALX_2)
         GBAL = Q BREAK(Q) Q | GBAL                            :(BALX_1)
BALX_2   PARENS LEN(1) . LP  RTAB(1) . PARENS  LEN(1) . RP    :F(BALX_3)
         GBAL = LP  (STAR | NULL) RP  |  GBAL                  :(BALX_2)
BALX_3   BALX = GBAL ARBNO(GBAL)
         $NAME = BALX                                          :(RETURN)
*
BALX_END
@

<<unit_test>>=
#!/usr/bin/bash
         exec "snobol4" "-b" "$0" "$@"
-INCLUDE 'BALX.INC'
         &CODE = 1
         &CODE = 0
END
@

<<>>=
-MODULE BALX
<<INCLUDES>>
-IN72
-STITL BALX
-EJECT
*
************************************************************************
*                                                                      *
*                  ######      #     #        #     #                  *
*                  #     #    # #    #         #   #                   *
*                  #     #   #   #   #          # #                    *
*                  ######   #     #  #           #                     *
*                  #     #  #######  #          # #                    *
*                  #     #  #     #  #         #   #                   *
*                  ######   #     #  #######  #     #                  *
*                                                                      *
* BALX                      BAL EXTENDED                               *
*                                                                      *
************************************************************************
*
* BALX.lss
*
<<BALX>>
*
* CE: .MSNOBOL4;
@
