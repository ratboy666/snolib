<h1>GOTOS - Keep the last 10 or so gotos of a program</h1>

The following trace function will give the last 10 or so gotos of a
SNOBOL4 program before it ends. This history is kept in the form of
a string so that it will get dumped if execution ends abnormally.
You can also send all error conditions to the statement:

[[
    ERROR OUTPUT = GOTOS_QUEUE
]]

The string [[GOTOS_QUEUE]] will have entries in the form:

[[
    F1>T1;F2>T2;...
]]

where entries are delimited by the semicolon.  [[FN]] is the 'from'
statement where a goto was made to the 'to' statement, [[TN]]. The most
recent goto is at the beginning of the string, and ages to the right.

From a note in the Dec., 1975 newsletter of SIGPLAN from Gideon Yuval,
Computer Science Dept., Hebrew University, Jerusalem, Israel.

Â© Copyright 1985 - Catspaw, Incorporated

<<GOTOS>>=
-PUBLIC GOTOS_()
         DEFINE('GOTOS_()')
         GOTOS_LENGTH = 119
         GOTOS_QUEUE = DUPL('.', GOTOS_LENGTH)
         GOTOS_PAT = POS(0) LEN(GOTOS_LENGTH) . GOTOS_QUEUE
         TRACE('STCOUNT', 'KEYWORD',, 'GOTOS_')
         &TRACE = 999999999
         &DUMP = 1                                          :(GOTOS_END)
*
GOTOS_   GOTOS_NO = &LASTNO
         EQ(GOTOS_LAST, GOTOS_NO)                             :S(RETURN)
         GOTOS_LAST = EQ(GOTOS_NO, GOTOS_LAST + 1) GOTOS_NO   :S(RETURN)
         (GOTOS_LAST '>' GOTOS_NO ';' GOTOS_QUEUE) ? GOTOS_PAT
         GOTOS_LAST = GOTOS_NO                                 :(RETURN)
*
GOTOS_END
@

<<unit_test>>=
#!/usr/bin/bash
         exec "snobol4" "-b" "$0" "$@"
-INCLUDE 'GOTOS.INC'
         &CODE = 1
         &CODE = 0
END
@

<<>>=
-STITL GOTOS
-EJECT
*
************************************************************************
*                                                                      *
*              #####   #######  #######  #######   #####               *
*             #     #  #     #     #     #     #  #     #              *
*             #        #     #     #     #     #  #                    *
*             #  ####  #     #     #     #     #   #####               *
*             #     #  #     #     #     #     #        #              *
*             #     #  #     #     #     #     #  #     #              *
*              #####   #######     #     #######   #####               *
*                                                                      *
* GOTOS        KEEP THE LAST 10 OR SO GOTOS OF A PROGRAM               *
*                                                                      *
************************************************************************
*
* GOTOS.lss
*
<<GOTOS>>
*
* CE: .MSNOBOL4;
@
