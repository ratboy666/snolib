<h1>ITERDIR - Directory (file) iterator</h1>

Use bq to gather directory data matching pattern. crack to crack
the string into an array of lines. atol to produce a linked list,
and return that list as the directory iterator.

We want to be able to specify directory patterns as either SNOBOL4
pattern or as OS globs. If the incoming [[W]] is a string, it will
be used as an OS glob match. If [[DATATYPE(W)]] is PATTERN, it will
be used to filter the OS directory list. A list of all files is
constructed. The list is walked, and entries that do not match the
pattern are discarded. To delete a node, we keep a trailing list
item [[L]]. [[NEXT(L) = NEXT(P)]] will then delete node [[P]]. Of
course, this doesn't work for the first element of the list. To
delete the first element, we simply forward step [[ITER_DIR]].
[[ITER_DIR = NEXT(ITER_DIR)]] will delete the first element.

A string cannot simply be converted to a PATTERN with CONVERT().
LEN(0) S will result in a pattern.

General note on iterators. The iter_() function returns an iterator
This iterator is passed to a value function v() which returns the
next thing being iterated. v() should fail if the end of iteration
is reached. The iterator is also passed to next() which returns a
new iterator which will return the next item in the iteration.
next_() may fail on the end of the iteration, but this is not
expected.

[[
An iterator should be used as:

    i = iter_type(args)
top   v = vali(i) :f(btm)
      do stuff with v
    i = next_type(i) :(top)
btm
]]

<h2>Uses</h2>
<a href="ATOL.html">ATOL</a><br>
<a href="BQ.html">BQ</a><br>
<a href="CHARS.html">CHARS</a><br>
<a href="CRACK.html">CRACK</a><br>
<a href="LINK.html">LINK</a><br>
<a href="VDIFFER.html">VDIFFER</a><br>

<<INCLUDES>>= 
-INCLUDE 'BQ.INC'
-INCLUDE 'CRACK.INC'
-INCLUDE 'ATOL.INC'
-INCLUDE 'CHARS.INC'
-INCLUDE 'LOGIC.INC'
-INCLUDE 'REPL.INC'
-INCLUDE 'VDIFFER.INC'
-INCLUDE 'LINK.INC'
@

<<ITERDIR>>=
-PUBLIC VALUE_DIR(), ITER_DIR(), NEXT_DIR(), READ_DIR()
*
         DEFINE('READ_DIR(W)P,O,V')
         DEFINE('ITER_DIR(W)P,L')
         DEFINE('VALUE_DIR(D)')
         DEFINE('NEXT_DIR(D)')                           :(END_ITER_DIR)
*
* READ DIRECTORY, RETURNING LIST OF FILES. WE USE -b TO HAVE ls ENCODE
* FILENAMES WITH C ESCAPES, AND THEN REPLACE THE ESCAPES IN EACH NAME.
* READ_DIR() CAN BE USED DIRECTLY, OR IS CALLED BY ITER_DIR(). THE
* ARGUMENT PASSED IS A FILENAME PATTERN THAT CAN BE APPLIED TO ls, (NOT
* A PATTERN).
*
* BE CAREFUL WITH THE WILDCARD PASSED - QUOTES (' AND ") WILL NOT WORK
* SO IT IS BETTER TO GATHER THE ENTIRE DIRECTORY CONTENTS AND FILTER.
* EVEN AN EMPTY DIRECTORY WILL HAVE . AND .. ENTRIES.
*
READ_DIR READ_DIR = ATOL(CRACK(BQ('/usr/bin/ls -a -1 -b 2>/dev/null ' W,
+                                 CHARS_NL),
+                              CHARS_NL))                    :F(FRETURN)
         P = READ_DIR
         O = ANY('01234567')
RD_DIR2  IDENT(P)                                             :S(RETURN)
RD_DIR3  VALUE(P) '\' (O O O) . V = CHAR(DIB(V, 8))          :S(RD_DIR3)
         VALUE(P) = REPL(VALUE(P), '\a', CHARS_BEL)
         VALUE(P) = REPL(VALUE(P), '\"', '"')
         VALUE(P) = REPL(VALUE(P), "\'", "'")
         VALUE(P) = REPL(VALUE(P), '\b' CHARS_BS)
         VALUE(P) = REPL(VALUE(P), '\e' CHARS_ESC)
         VALUE(P) = REPL(VALUE(P), '\f' CHARS_FF)
         VALUE(P) = REPL(VALUE(P), '\n' CHARS_NL)
         VALUE(P) = REPL(VALUE(P), '\r' CHARS_CR)
         VALUE(P) = REPL(VALUE(P), '\t' CHARS_HT)
         VALUE(P) = REPL(VALUE(P), '\v' CHARS_VT)
         VALUE(P) = REPL(VALUE(P), '\\', '\')
         P = NEXT(P)                                          :(RD_DIR2)
*
VALUE_DIR
         VALUE_DIR = VALUE(VDIFFER(D))              :S(RETURN)F(FRETURN)
*
ITER_DIR IDENT(DATATYPE(W), 'PATTERN')                      :S(ITERDIR2)
*
         ITER_DIR = READ_DIR(W)                     :S(RETURN)F(FRETURN)
*
ITERDIR2 ITER_DIR = READ_DIR()                               :F(FRETURN)
         P = ITER_DIR
ITERDIR3 IDENT(P)                                             :S(RETURN)
         VALUE(P) W                                         :S(ITERDIR4)
         NEXT(VDIFFER(L)) = NEXT(P)
         ITER_DIR = IDENT(L) NEXT(ITER_DIR)
         P = IDENT(L) ITER_DIR                              :S(ITERDIR3)
         P = NEXT(P)                                         :(ITERDIR3)
ITERDIR4 L = P; P = NEXT(P)                                  :(ITERDIR3)
*
NEXT_DIR NEXT_DIR = NEXT(VDIFFER(D))                :S(RETURN)F(FRETURN)
*
END_ITER_DIR
@

<<unit_test>>=
#!/usr/bin/bash
         exec "snobol4" "-b" "$0" "$@"
-INCLUDE 'ITERDIR.INC'
         &CODE = 1
         DI = ITER_DIR(FENCE 'W')
TOP1       OUTPUT = VALUE_DIR(DI)                               :F(BTM1)
         DI = NEXT_DIR(DI)                                       :(TOP1)
BTM1
*
         DI = ITER_DIR('c*')
TOP2       OUTPUT = VALUE_DIR(DI)                               :F(BTM2)
         DI = NEXT_DIR(DI)                                       :(TOP2)
BTM2
         &CODE = 0
END
@

<<>>=
-MODULE ITERDIR
<<INCLUDES>>
-IN72
-STITL ITERDIR
-EJECT
*
************************************************************************
*                                                                      *
*        ###  #######  #######  ######   ######   ###  ######          *
*         #      #     #        #     #  #     #   #   #     #         *
*         #      #     #        #     #  #     #   #   #     #         *
*         #      #     #####    ######   #     #   #   ######          *
*         #      #     #        #   #    #     #   #   #   #           *
*         #      #     #        #    #   #     #   #   #    #          *
*        ###     #     #######  #     #  ######   ###  #     #         *
*                                                                      *
* ITERDIR                   DIRECTORY ITERATOR                         *
*                                                                      *
************************************************************************
*
* ITERDIR.lss
*
<<ITERDIR>>
*
* CE: .MSNOBOL4;
@
