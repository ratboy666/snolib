<h1>READFILE - Read complete file into string</h1>

Reads a complete file into a string. No conversions are done. Uses
'B,65536' (binary, 64k byte buffer) for the INPUT() function. This
is specific to CSNOBOL4.

This is used to bring in content from disk (say, images, templates
or html). This content can then be left in memory if needed.

Tested w/ 1024..256K buffer size. Default 64K buffer.

If used with SPITBOL, reads a line at a time, adding newlines.

Also implements WRITEFILE(). Note that SPITBOL will form lines and
thus can only read or write text. CSNOBOL4 will read and write an
arbitrary binary string.

<<INCLUDES>>=
-INCLUDE 'CHARS.INC'
-INCLUDE 'SYSTEM.INC'
-INCLUDE 'BRKREM.INC'
@

<<READFILE>>=
         DEFINE('WRITEFILE(FILE,S)F,U,E,T')
         DEFINE('READFILE(FILE)F,U,E')                   :(READFILE_END)
*
WRITEFILE
         SYSTEM() 'CSNOBOL4'                              :S(WRITEFILE3)
         U = 20
         OUTPUT(.F, U, FILE)                                 :F(FRETURN)
WRITEFILE2
         S BRKREM(CHARS_NL) . T (CHARS_NL | NULL) =
         F = T
         IDENT(S)                             :S(READFILE4)F(WRITEFILE2)
WRITEFILE3
         U = IO_FINDUNIT()
         OUTPUT(.F, U, 'B,65536', FILE)
WRITEFILE4
         T = LT(SIZE(S), 65536) S                         :F(WRITEFILE6)
         S =
WRITEFILE5
         F = T
         IDENT(S)                             :S(READFILE4)F(WRITEFILE4)
WRITEFILE6
         S LEN(65536) . T REM . S                          :(WRITEFILE5)
*
READFILE SYSTEM() 'CSNOBOL4'                               :S(READFILE3)
         U = 20
         E = CHARS_NL
         INPUT(.F, U, FILE)                      :S(READFILE2)F(FRETURN)
READFILE3
         U = IO_FINDUNIT()                                   :F(FRETURN)
         INPUT(.F, U, 'B,65536', FILE)                       :F(FRETURN)
READFILE2
         READFILE = READFILE F E                           :S(READFILE2)
READFILE4
         ENDFILE(U)
         DETACH(.F)                                            :(RETURN)
*
READFILE_END
@

<<unit_test>>=
#!/usr/bin/bash
         exec "snobol4" "-b" "$0" "$@"
-INCLUDE 'READFILE.INC'
         &CODE = 1
         OUTPUT = READFILE('sample.tmpl')                        :F(END)
         &CODE = 0
END
@

<<>>=
<<INCLUDES>>
-STITL READLINE
-EJECT
*
************************************************************************
*                                                                      *
*  ######   #######     #     ######   #######  ###  #        #######  *
*  #     #  #          # #    #     #  #         #   #        #        *
*  #     #  #         #   #   #     #  #         #   #        #        *
*  ######   #####    #     #  #     #  #####     #   #        #####    *
*  #   #    #        #######  #     #  #         #   #        #        *
*  #    #   #        #     #  #     #  #         #   #        #        *
*  #     #  #######  #     #  ######   #        ###  #######  #######  *
*                                                                      *
* READFILE          READ COMPLETE FILE INTO STRING                     *
*                                                                      *
************************************************************************
*
* READFILE.lss
*
<<READFILE>>
*
* CE: .F.MSNOBOL4;
@
