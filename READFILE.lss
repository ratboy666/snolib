<h1>READFILE - Read complete file into string</h1>

Reads a complete file into a string. No conversions are done. Uses
'B,65536' (binary, 64k byte buffer) for the INPUT() function. This
is specific to CSNOBOL4.

If used with SPITBOL, reads a line at a time, adding newlines.

<h2>Uses</h2>
<a href="BRKREM.html">BRKREM</a><br>
<a href="CHARS.html">CHARS</a><br>
<a href="SYSTEM.html">SYSTEM</a><br>

<<INCLUDES>>=
-INCLUDE 'BRKREM.INC'
-INCLUDE 'CHARS.INC'
-INCLUDE 'SYSTEM.INC'
@

<<READFILE>>=
-PUBLIC READFILE()
*
         DEFINE('READFILE(FILE)F,U,E')                   :(READFILE_END)
*
READFILE SYSTEM() 'CSNOBOL4'                               :S(READFILE3)
         U = 20
         E = CHARS_NL
         INPUT(.F, U, FILE)                      :S(READFILE2)F(FRETURN)
READFILE3
         U = IO_FINDUNIT()                                   :F(FRETURN)
         INPUT(.F, U, 'B,65536', FILE)                       :F(FRETURN)
READFILE2
         READFILE = READFILE F E                           :S(READFILE2)
READFILE4
         ENDFILE(U)
         DETACH(.F)                                            :(RETURN)
*
READFILE_END
@

<<unit_test>>=
#!/usr/bin/bash
         exec "snobol4" "-b" "$0" "$@"
-INCLUDE 'READFILE.INC'
         &CODE = 1
         OUTPUT = READFILE('sample.tmpl')                        :F(END)
         &CODE = 0
END
@

<<>>=
-MODULE READFILE
<<INCLUDES>>
-IN72
-STITL READLINE
-EJECT
*
************************************************************************
*                                                                      *
*  ######   #######     #     ######   #######  ###  #        #######  *
*  #     #  #          # #    #     #  #         #   #        #        *
*  #     #  #         #   #   #     #  #         #   #        #        *
*  ######   #####    #     #  #     #  #####     #   #        #####    *
*  #   #    #        #######  #     #  #         #   #        #        *
*  #    #   #        #     #  #     #  #         #   #        #        *
*  #     #  #######  #     #  ######   #        ###  #######  #######  *
*                                                                      *
* READFILE          READ COMPLETE FILE INTO STRING                     *
*                                                                      *
************************************************************************
*
* READFILE.lss
*
<<READFILE>>
*
* CE: .F.MSNOBOL4;
@
