<h1>ROMAN - Roman numerals</h1>

[[ROMAN(N)]] will return the roman numeral representation of the
integer [[N]]. [[ARABIC(R)]] will return the arabic numeral for the
roman numeral [[R]].

<b>N<small>OTE</small></b> - [[0 < N < 4000]].

<<ROMAN>>=
-PUBLIC ROMAN()
         DEFINE('ROMAN(N)T')                                :(ROMAN_END)
*
ROMAN    N RPOS(1) LEN(1) . T =                               :F(RETURN)
         '0,1I,2II,3III,4IV,5V,6VI,7VII,8VIII,9IX,'
+           T BREAK(',') . T                                 :F(FRETURN)
         ROMAN = REPLACE(ROMAN(N), 'IVXLCDM', 'XLCDM**') T
+                                                   :S(RETURN)F(FRETURN)
*
ROMAN_END
*
-PUBLIC ARABIC()
         DEFINE('ARABIC(R)T,X')                            :(ARABIC_END)
*
ARABIC   R RPOS(1) LEN(1) . T =                               :F(RETURN)
         'M1000,D500,C100,L50,X10,V5,I1,' T BREAK(',') . T   :F(FRETURN)
         T = LT(T, X) -T
         ARABIC = ARABIC + (X = T)                             :(ARABIC)
*
ARABIC_END
@

<<unit_test>>=
#!/usr/bin/bash
         exec "snobol4" "-b" "$0" "$@"
-INCLUDE 'ROMAN.INC'
         &CODE = 1
LOOP     I = I + 1
         R = ROMAN(I)
         N = ARABIC(R)
         OUTPUT = NE(I, N) 'FAIL: ' I ' ' R ' ' N
         EQ(I, N)                                                :F(END)
         LT(I, 3999)                                            :S(LOOP)
         &CODE = 0
END
@

<<>>=
-STITL ROMAN
-EJECT
*
************************************************************************
*                                                                      *
*             ######   #######  #     #     #     #     #              *
*             #     #  #     #  ##   ##    # #    ##    #              *
*             #     #  #     #  # # # #   #   #   # #   #              *
*             ######   #     #  #  #  #  #     #  #  #  #              *
*             #   #    #     #  #     #  #######  #   # #              *
*             #    #   #     #  #     #  #     #  #    ##              *
*             #     #  #######  #     #  #     #  #     #              *
*                                                                      *
* ROMAN                      ROMAN NUMERAL                             *
*                                                                      *
************************************************************************
*
* ROMAN.lss
*
<<ROMAN>>
*
* CE: .MSNOBOL4;
@
